buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        google()
        jcenter()
    }

    apply from: 'dependencies.gradle'

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:${versions.binaryCompatibilityValidator}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokkaGradlePlugin}"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:${versions.ktlintGradle}"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:${versions.spotlessGradlePlugin}"
        classpath "org.jacoco:org.jacoco.core:${versions.jacocoGradlePlugin}"
        classpath "org.jetbrains.kotlinx:atomicfu-gradle-plugin:${versions.atomicFuPlugin}"
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.13.0"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.1"
    }
}

apply from: 'dependencies.gradle'
apply plugin: 'binary-compatibility-validator'

apiValidation {
    ignoredProjects += ["sample", "integration-tests", "processor"]
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    ktlint {
        version = versions.ktlint
    }


    // Workaround to prevent Gradle from stealing focus from other apps during tests run/etc.
    // https://gist.github.com/artem-zinnatullin/4c250e04636e25797165
    tasks.withType(JavaForkOptions) {
        jvmArgs '-Djava.awt.headless=true'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = '1.8'
            freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn", '-Xjvm-default=compatibility']
        }
    }
}

subprojects { Project project ->
    project.plugins.withId('com.android.library') {
        project.android {
            compileSdkVersion versions.compileSdk
            buildToolsVersion versions.buildTools

            defaultConfig {
                minSdkVersion versions.minSdk
                targetSdkVersion versions.targetSdk
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
        }
    }
}

ext {
    // POM file
    GROUP = "com.dropbox.kaiken"
    VERSION_NAME = "1.0.0"
    POM_PACKAGING = "pom"
    POM_DESCRIPTION = "Store4 is built with Kotlin Coroutines"

    POM_URL = "https://github.com/dropbox/kaiken/"
    POM_SCM_URL = "https://github.com/dropbox/Store/"
    POM_SCM_CONNECTION = "scm:git:https://github.com/dropbox/kaiken.git"
    POM_SCM_DEV_CONNECTION = "scm:git:git@github.com:dropbox/kaiken.git"

    POM_LICENCE_NAME = "Apache License"
    POM_LICENCE_URL = "http://www.apache.org/licenses/LICENSE-2.0"
    POM_LICENCE_DIST = "repo"

    POM_DEVELOPER_ID = "dropbox"
    POM_DEVELOPER_NAME = "Dropbox"
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target 'src/**/*.kt'
        }
    }
}

dependencies {

}
